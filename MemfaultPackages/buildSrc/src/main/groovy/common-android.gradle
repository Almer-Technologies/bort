// Standardizes all our android gradle plugin options that apply for every
// Android module.
//
// Requires that either the 'com.android.application' or 'com.android.library'
// plugins have been applied before applying this buildscript plugin on top.
//
// In the future we can split into 'common-android-library' and
// 'common-android-app', etc., to better specify specific configurations.

android {
    compileSdkVersion versions.compileSdk

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
        freeCompilerArgs += "-Xopt-in=kotlin.time.ExperimentalTime"
        freeCompilerArgs += "-Xjvm-default=all"
    }

    lint {
        abortOnError true
    }

    testOptions {
        // Needed to unit test against AIDL-generated code
        unitTests.returnDefaultValues = true
        unitTests.all {
            useJUnitPlatform()
            testLogging.showStandardStreams = true
        }
    }
}
