import static com.memfault.bort.buildsrc.BortSettingsDownloaderKt.getBortSettingsAssetsPath

apply plugin: 'com.android.library'
apply plugin: 'com.squareup.anvil'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply from: rootProject.file("properties.gradle")
apply plugin: 'common-android'

def generatedSettingsAssets = getBortSettingsAssetsPath(project.rootProject.rootDir)

android {
    defaultConfig {
      // Used by BuildConfigSdkVersionInfo:
        buildConfigField "String", "APP_VERSION_NAME", "\"${loadVersionName()}\""
        buildConfigField "int", "APP_VERSION_CODE", "${loadVersionCode()}"
        buildConfigField "String", "UPSTREAM_VERSION_NAME", "\"${upstreamVersionName()}\""
        buildConfigField "int", "UPSTREAM_VERSION_CODE", "${upstreamVersionCode()}"
        buildConfigField "String", "UPSTREAM_GIT_SHA", "\"${upstreamGitSha()}\""
        buildConfigField "String", "CURRENT_GIT_SHA", "\"${currentGitSha()}\""
        buildConfigField "String", "BORT_APPLICATION_ID", "\"" + bortProperty("BORT_APPLICATION_ID") + "\""
        buildConfigField "String", "BORT_OTA_APPLICATION_ID", "\"" + bortProperty("BORT_OTA_APPLICATION_ID") + "\""
        buildConfigField "int", "CLIENT_SERVER_PORT", bortProperty("CLIENT_SERVER_PORT")
        buildConfigField "String", "CLIENT_SERVER_HOST", "\"" + bortProperty("CLIENT_SERVER_HOST") + "\""
        buildConfigField "Boolean", "OTA_AUTO_INSTALL", bortProperty("OTA_AUTO_INSTALL")
        buildConfigField "Boolean", "ALLOW_PROJECT_KEY_CHANGE", bortProperty("ALLOW_PROJECT_KEY_CHANGE")
        buildConfigField "String", "MEMFAULT_PROJECT_API_KEY", "\"" + bortProperty("MEMFAULT_PROJECT_API_KEY") + "\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    sourceSets {
        main {
            aidl.srcDirs = ['../../MemfaultDumpster/']
            assets.srcDirs += [generatedSettingsAssets]
        }
    }

    namespace 'com.memfault.bort.shared'
}

kapt {
    correctErrorTypes true
    arguments {
        // This is necessary because of the way we bind Anvil's component to Hilt's.
        arg("dagger.hilt.disableModulesHaveInstallInCheck", "true")
    }
}

dependencies {
    implementation project(':reporting-lib')

    implementation deps.android.work
    implementation deps.dagger.hilt
    implementation deps.jblake2
    implementation deps.kotlin.stdlib
    implementation deps.kotlin.coroutines
    implementation deps.kotlin.coroutines.android
    implementation deps.okhttp
    implementation deps.result
    implementation deps.serialization.json
    implementation deps.serialization.converter

    kapt deps.dagger.hilt.compiler

    testImplementation deps.test.junit.api
    testImplementation deps.test.junit.jupiter.engine
    testImplementation deps.test.coroutines
    testImplementation deps.test.mockk
}
