apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "../properties.gradle"

android {
    compileSdkVersion versions.compileSdk

    defaultConfig {
        applicationId "com.memfault.usagereporter"
        manifestPlaceholders = [
                // For development purposes (allows UsageReporter to be updated using `adb install`):
                bortUsageReporterPersistent: bortProperty("BORT_USAGE_REPORTER_PERSISTENT") ?: "true",
        ]

        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode loadVersionCode() as Integer
        versionName loadVersionName() as String

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }

        // Used for CI. Do not use for production.
        releaseTest {
            initWith release
        }
    }

    variantFilter { variant ->
        if (variant.buildType.name.endsWith('debug')) {
            variant.setIgnore(true);
        }
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
        freeCompilerArgs += "-Xopt-in=kotlin.time.ExperimentalTime"
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            useJUnitPlatform()
        }
    }
  namespace 'com.memfault.usagereporter'
}

dependencies {
    implementation project(':bort-shared')
    implementation project(':reporting-lib')

    implementation deps.kotlin.coroutines
    implementation deps.kotlin.stdlib
    implementation deps.android.preference
    implementation deps.result

    testImplementation deps.test.junit.api
    testImplementation deps.test.junit.jupiter.engine
    testImplementation deps.test.coroutines
    testImplementation deps.test.mockk
}

afterEvaluate {
    tasks.named("assembleRelease").configure {
        doLast {
            copy {
                from file("$buildDir/outputs/apk/release/reporter-release-unsigned.apk")
                into rootDir
                rename 'reporter-release-unsigned.apk', 'MemfaultUsageReporter.apk'
            }
        }
    }
}
